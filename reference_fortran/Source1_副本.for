C   Simulation of entrained-flow gasfication process
C   Compartment-in-series model
C   ***shrinking-core model***
C   ***to specify the flow direction
C   ***the definition of "direct" given in subroutine vsolid is used
C   for upward flow, 'direct = 1.0'
C   for downward flow, 'direct = -1.0'

      parameter rg = 82.06
      real ks, kv, kdiff, kover
      real n2p, n2p1, n2p2, n2p3, n2p5
      real n2mol
      character*12 name
      dimension ht(20),htup(30),cp(30),dtm(5)
      dimension tgas(5), ftg(4),hbl(4),tsolid(5),fts(5),dfts(5),rk(5)
      dimension x1(120,2),y1(120,2),npt1(2),ic1(2)
      dimension x2(120,4),y2(120,4),npt2(4),ic2(4)
      dimension x3(120,3),y3(120,3),npt3(3),ic3(3)
      dimension x4(120,1),y4(120,1),npt4(1),ic4(1)
      dimension x5(120,2),y5(120,2),npt5(2),ic5(2)
      
      common /s1/ fcoal,foxy,fsteam
      common /s2/ tg, ts, dens, deng, cps, pt
      common /s3/ tfcoal, xmois
      common /a1/ yc,yoxy,yh,ys,yn,ycr,yoxyr,yhr,ysr,ynr
      common /a2/ xh2,xco,xco2,xh2s,xh2o,xn2,xch4,xtar
      common /a3/ goxy,gsteam,gco2,gco,gh2,gh2s,gn2,gch4
C
C
      data hl/30./ !combustion heat percent transfered to the wall
      data a,b,c,q/4.92e5,8900.,0.14,1.25/
      data vis,at,dt,pt,height/6.e-4,18241.5,152.4,24.,330./ !initial code data
      
      data npt1,ic1/120,120,'$$$$','****'/
      data npt2,ic2/120,120,120,120,'1111','2222','3333','4444'/
      data npt3,ic3/120,120,120,'5555','6666','7777'/
      data npt4,ic4/120,'%%%%'/
      data npt5,ic5/120,120,'gggg','ssss'/
      
      namelist /readdata/name,ta,tsteam,toxy,yoxy,yh,yc,ys,yn,yash,
     &                    xmois,foxy,fsteam,tfcoal 

      open(unit=2000,file='input.txt',action='read')
	  
10    read(2000,nml=readdata)
C
      write(6,readdata)
C
C   ***FC is the fixed carbon in dmmf basis***
      fc = 65.0
      densi = 1.18
      ri = 1.75e-2
      cps = 0.45
      
      cpoxy = 8.724
      cpstm = 11.34
      cpco2 = 13.83175
      cpco = 8.4765
      cpch4 = 20.772
      cph2 = 7.73
      cph2s = 4.77
      cpn2 = 8.39675
      
 
C   fcoal is the feed rate of coal in dmmf basis
      fcoal = tfcoal*(1.-yash)*(1.-xmois)

C   ***tohup is cal/g dmmf coal feed
C   ***fcoal*tohup is cal flow through this compartment /sec      
C   ***subdwl is g coal consumed in this compartment /g dmmmf coal
C   ***fcoal*subdwl/100 is g coal consumed in this compartment /sec
C   ***rheat1 is cal produced in this compartment /g dmmf coal
C   ***rheat1*fcoal is cal produced in this compartment /sec
C   ***cwl's are g carbon consumed in this compart /g dmmf
C   ***rheat is cal generated in this compartment /g dmmf coal feed
C   ***rheat*fcoal is cal generated in this compartment /sec
C   ***fgs is the fraction of heat generated by CO and H2 combustion
C   ***that back radiated to the solid surface from the frame front
C   ***in addition to those from the bulk gas phase
      fgs = 0.25
      swell = 1.0
      ef = 0.9
      sigma = 1.355e-12
      fash = yash/(1.-yash)
      
C   ***rs is the fraction of coal sulphur that does not reacted 
C   ***into gaseous product but deposited together with ash***
C   ***rn is the fraction of coal nitrogen that does not reacted
C   ***into gaseous product but deposited together with ash***

      rs = 0.0
      ys = ys*(1.-rs)
      rn = 0.0
      yn = yn*(1.-rn)
    
      delh = 25.
C
C   ***'fwgs' is the fraction of heat released by water-gas-shift  
C   ***that is adsorpted by the solid phase
      fwgs = 1.0
      
      call volatl(yc,yoxy,yh,yn,ys,yash,fc,fo,fh,fn,fs,vh2,r1,r2,vm0)
      vm0 = vm0*(1.-0.066*alog(pt))
      swl = Q*vm0*(1.-c)
      
      ycr = yc - vm0*(xco*12./28.+xco2*12./44.+xch4*12./16.+xtar*
     & 72./78.)/100.
      yoxyr = yoxy - vm0*(xco2*32./44.+xco*16./28+xh2o*16./18.)/100.
      yhr = yh - vm0*(xh2 +xh2s*2./34. + xh2o*2./18.+xch4*4./16.+xtar
     & *6./78.)/100.
      ysr = ys - xh2s*vm0*32./(34.*100.)
      ynr = yn - xn2*vm0/100.
      
      floxy = yoxyr/ycr
      flh = yhr/ycr
      fln = ynr/ycr
      fls = ysr/ycr
      
      alpha = ycr/12.
      beta = yhr/1.
      gama = yoxyr/16.
      delta = ynr/14.
      epsn = ysr/32.

      write(6,547) alpha,beta,gama,delta,epsn
  547 format(//2x,'alpha=',f15.5,2x,'beta=',f15.5,2x,'gama=',f15.5,
     & 2x,'delta=',f15.5,2x,'epsn=',f15.5)

      densp = densi*(1.+fash-swl/100.)/((1.+fash)*swell)
      rp = ri*(swell**0.333)
      
      goxyin = foxy*tfcoal
      gh2oin = (fsteam+xmois)*tfcoal
      
C   ***enthin is the total inlet enthalpy
      call heatup(1,toxy,298.,htup,cp)
      call heatup(2,tsteam,298.,htup,cp)
      enthg = htup(1)*goxyin/32. + htup(2)*tfcoal*fsteam/18.
c      enthg = htup(1)*goxyin/32. + (htup(2)-10520.)*tfcoal*fsteam/18.
      call heatup(2,ta,298.,htup,cp)
      enths = cps*fcoal*(1.+fash)*(ta-298.)+tfcoal*xmois*
     & (htup(2)-10520.)/18.
      enthin = enthg+enths

C   ***nc is the number of step sizes in each gas compartment
      nc = 3
      tmole = goxyin/32. + gh2oin/18.
      poxyin = pt*goxyin/(32.*tmole)
      ph2oin = pt*gh2oin/(18.*tmole)
      
      gco2in = 0.
      pco2in = 0.
      gcoin = 0.
      pcoin = 0.
      gh2sin = 0.
      ph2sin = 0.
      gh2in = 0.
      ph2in = 0.
      gn2in = 0.
      pn2in = 0.
      gch4in = 0.
      pch4in = 0.
      
      tsin = ta !particle inlet temperature
      timein = 0.
      hsin = 0.
      wlin = 0.
      usin = 300. !inlet solid velocity [cm/sec]
      gtarin = 0.
      
      tgas(1) = 2800.
      tgas(2) = 2900.
      
      wlcp = swl
      
      tg1 = (goxyin*cpoxy*toxy/32.+gh2oin*cpstm*tsteam/18.)/
     & (goxyin*cpoxy/32.+gh2oin*cpstm/18.)
      
      x1(1,1) = 0.
      x1(1,2) = 0.
      y1(1,1) = tg1
      y1(1,2) = ta
      x2(1,1) = 0.
      x2(1,2) = 0.
      x2(1,3) = 0.
      x2(1,4) = 0.
      y2(1,1) = (gh2oin/18.)/tmole
      y2(1,2) = 0.
      y2(1,3) = 0.
      y2(1,4) = 0.
      x3(1,1) = 0.
      x3(1,2) = 0.
      x3(1,3) = 0.
      y3(1,1) = 0.
      y3(1,2) = 0.
      y3(1,3) = 0.
      x4(1,1) = 0.
      y4(1,1) = 0.
      x5(1,1) = 0.
      x5(1,2) = 0.
      y5(1,1) = tmole*rg*tg1/(at*pt)
      y5(1,2) = usin
      
      i = 1
      j = 1
      iterg = 1
      m = 1
      r = (ri+rp)/2.

C   ***gas-wall heat transfer***
C   ***u0 is the overall heat transfer coeff. between gas and wall
C   ***for gasification zone simulated as a heat exchanger, cal/sec
C   ***for gasification zone simulated as a heat exchanger, cal/(sec.cm^2K)
      u0 = 3.4e-3

      tw = 1700.
      
300   tg = tgas(i)
      if (tg .le. 300.) tg = ts+100.
      time = timein
      hs = hsin
      usi = usin
      wl = wlin
      ts = tsin
      goxy = goxyin
      gsteam = gh2oin
      gco2 = gco2in
      gco = gcoin
      gh2 = gh2in
      gh2s = gh2sin
      gn2 = gn2in
      gch4 = gch4in
      poxy = poxyin
      psteam = ph2oin
      pco2 = pco2in
      pco = pcoin
      ph2 = ph2in
      ph2s = ph2sin
      pn2 = pn2in
      pch4 = pch4in
      gtar = gtarin
      
      k = 1
      tmole = goxy/32.+gsteam/18.+gco2/44.+gco/28.+gh2/2.+gh2s/34.+
     & gn2/28. + gch4/16.
      gsum = goxy+gsteam+gco2+gco+gh2+gh2s+gn2+gch4
      
      rheat1 = 0.
      rheat2 =0.
      rheat3 = 0.
      rhts = 0.
      subdwl = 0.
      rht = 0.
      rk(3) = 0.
      dwl = (swl-wl)/swl
      if(dwl .le. 0.08) goto 500
      
140   if(ts .gt. 600.) r = rp
      dp = 2.*r
      dens = densi*(1.+fash-wl/100.)/((1.+fash)*swell)
      call vsolid(delh,usi,tmole,gsum,dp,tg,pt,at,vis,dens,deltim,us,ug)
      usi = us
      
110   condut = (7.7e-7)*((tg+ts)**0.75)
      ct = -(3./(dens*cps*r))*(condut/r + ef*sigma*4.*tg**3)*deltim
      if(abs(ct) .gt. 25.) then
      ect = 1.0e-12
      else 
      ect = exp(ct)
      endif
      delts = (tg-(tg-ts)*ect)-ts
      tsm = ts+delts/2.
      ts = ts+delts
      if (tsm .gt. 1250.) tsm = 1250.
      
      time = time + deltim
      hs = hs + delh
      agu1 = -b/tsm
      if(abs(agu1) .le. 25.) then
      eagu1 = exp(agu1)
      else
      eagu1 = 0.
      endif
      ak = a*eagu1
      
      agu2 = -ak*deltim
      if(abs(agu2) .le. 25.) then
      eagu2 = exp(agu2)
      else
      eagu2 = 0.
      endif      
      delwl = (swl-(swl-wl)*eagu2)-wl
      wl = wl+delwl
      
      write(6,123) time,ts,wl,hs,tg,delwl
123   format(///2x,'time=',f15.5,5x,'ts=',f15.5,5x,'wl=',f15.5,2x,
     & 'hs=',f15.5,2x,'tg=',f15.5,2x,'delwl=',f15.6)
      
      subdwl = subdwl+delwl
      dens = densi*(1.+fash-wl/100.)/((1.+fash)*swell)
      dwl = (swl-wl)/swl
      if(dwl.le.0.08) wlcp=wl
      if(k.ge.nc) goto 400
      k = k+1
      goto 110
      
400   ycr = yc-(wlcp/100.)*(xco*12./28.+xco2*12./44.+xch4*12./16.+
     & xtar*72./78.)
      
      do n=1,4
      call heat(n,tg,ht)
      enddo
      
      call pyroly(subdwl,swl,goxy,rheat1,doxy,waterp,co2p,h2sp,n2p,cop,
     & h2p,ch4p,tarp)
      goxyex = goxy-doxy*fcoal
      if(goxyex .lt. 0.) then
      goxy = 0.
      else
      goxy = goxyex
      endif
      gsteam = gsteam+waterp*fcoal
      gco2 = gco2+co2p*fcoal
      gh2s = gh2s + h2sp*fcoal
      gn2 = gn2 + n2p*fcoal
      gco = gco + cop*fcoal
      gh2 = gh2 + h2p*fcoal
      gch4 = gch4 + ch4p*fcoal
      gtar = gtar + tarp*fcoal
      gsum = goxy+gsteam+gco2+gh2s+gn2+gco+gh2+gch4
      tmole = goxy/32.+gsteam/18.+gco2/44.+gco/28.+gh2/2.+gh2s/34.
     & +gn2/28. + gch4/16.
      xcarb =100.*(gco/28.+gco2/44.+gch4/16.+gtar*6./78.)*12./(fcoal*yc)
      poxy = pt*(goxy/32.)/tmole
      psteam = pt*(gsteam/18.)/tmole
      pco2 = pt*(gco2/44.)/tmole
      pco = pt*(gco/28.)/tmole
      ph2 = pt*(gh2/2.)/tmole
      pch4= pt*(gch4/16.)/tmole
      ph2s =pt*(gh2s/34.)/tmole
      pn2 = pt*(gn2/28.)/tmole
       
      call enthal(tg,ts,fcoal,fash,wl,cps,enth)
      tohup = (enth-enthin)/fcoal
      if(poxyin .gt. 0.05) then
      hloss = fcoal*abs(rheat1)*hl/100.
      else
      tw = 2100.-600.*hs/height
      hloss = u0*3.14*dt*float(nc)*delh*(tg-tw)
      endif
      htexc = -rheat1-tohup-hloss/fcoal
420   do n=1,8
      call heatup(n,tg,298.,htup,cp)
      enddo
!add ch4 contribution
      deltg = (htexc*fcoal)/(goxy*cp(1)/32.+gsteam*cp(2)/18.+gco2*cp(3)/
     & 44.+gco*cp(4)/28.+gch4*cp(5)/16.+gh2*cp(6)/2.+gh2s*cp(7)/34. 
     & +gn2*cp(8)/28.)
      errtg = deltg/tg
      dhtex = htexc/tohup
      if(abs(errtg) .le. .02) goto 800
      if(abs(htexc) .le. 5.0) goto 800
      if(abs(dhtex) .le. 0.1) goto 800
      dwl = (swl-wl)/swl
      if(m.eq.1 .or. dwl.ge.0.08)  goto 302
      if(iterg.ge.5) then
          delh = delh/2.
          tgas(1) = tgin-30.
          i=1
          goto 300      
      else
          iterg = iterg+1
      endif

C   use of wegstein method for the search of correct tg
C   search for stable balance of heat generation and heat requirement
302   ftg(i) = tgas(i) + deltg
      hbl(i) = htexc
      
      if(i .eq. 2) goto 319
      if(i .eq. 3) goto 329
      if(m .eq. 1) then
      tgas(2) = tgas(1)+100.
      else      
      tgas(2) = tg+deltg/2.
      if(tgas(2) .gt. 4000.) tgas(2)=tg+deltg/5.
      endif
      i = 2
      goto 300

319   checkg = hbl(1)*hbl(2)
      if(checkg .gt. 0.) then
      tgas(3) = (tgas(1)*ftg(2)-tgas(2)*ftg(1))/(tgas(1)-tgas(2)+ftg(2)
     & -ftg(1))
      if(tgas(3) .le. 200.) tgas(4) = 200.
      if(tgas(3) .ge. 4000.) tgas(4) = 4000.
      else
      tgas(3) = (tgas(1)*hbl(2)-tgas(2)*hbl(1))/(hbl(2)-hbl(1))
      endif
      i = 3
      goto 300
      
329   check1 = hbl(1)*hbl(3)
      if(check1 .gt. 0.) then
      tgas(1) = tgas(2)
      ftg(1) = ftg(2)
      hbl(1) = hbl(2)
      endif 
      tgas(2) = tgas(3)
      ftg(2) = ftg(3)
      hbl(2) = hbl(3)
      goto 319
      

   
C   Several original codes have been deleted/depreciated
500   rhts = 0.
      rheat2 = 0.
      r = rp
      dp = 2.*r
      if(wl.ge.99.95) wl = 99.95
      ti = ts
      dens = densi*(1.+fash-wl/100.)/((1.+fash)*swell)
      y = ((1.-wl/100.)/(1.-swl/100.))**0.333
      rc = rp*y
      call vsolid(delh,usi,tmole,gsum,dp,tg,pt,at,vis,dens,deltim,us,ug)
      usi = us
      if(goxy.gt.0.) goto 505
      goxy = 0.
      poxy = 0.
      goto 600
C   ***when oxygen is still existed***
C   if oxygen is still enough the partial pressure of CO is almost
C   zero and the water-gas-shift reaction can be neglected
C
505   tsolid(1) = ts
      if(wl.ge.99.95) goto 560
      if(xcarb .ge. 99.75) goto 560
      
      y = ((1.-wl/100.)/(1.-swl/100.))**.333
      rc = rp*y
      r = rp
      dp = 2.*r
      call heat(1,tg,ht)
      call heat(2,tg,ht)
C   ***Runge-Kutta-Gill method
      do l = 1,4
      ts = tsolid(l)
      if(ts .le.0.) goto 506
      call combus(tg,ts,pt,rp,poxy,swl,y,densp,rate,cwl,phi,qrh,fash,
     &deltim,q1,q2)
      call cbstm(deltim,psteam,ph2,pco,rp,pt,densp,tg,ts,swl,y,fash,
     &rate2,cwl2,qcsm,wl)
      call cbco2(pco2,deltim,wl,swl,rp,pt,densp,tg,ts,fash,y,rate3,
     &cwl3,qcbco2)
      qccog = (rate3/12.)*(2.*ht(2)+ht(1)*(beta/2.-gama-epsn)/alpha)
      qcsg = (rate2/12.)*(ht(2)+ht(1)*(alpha-gama+beta/2.-epsn)/alpha)
      qcomg = (rate/12.)*(2.-2./phi)*ht(2)
      condut = 7.7e-7 * ((ts+tg)**.75)
      rk(l)=(3./(dens*cps*r))*(condut*(tg-ts)/r+ef*sigma*(tg**4-ts**4)+
     &qrh*rate-qcsm*rate2/12.-qcbco2*rate3/12.-fgs*(qcomg+qcsg+qccog))
      goto 507
506   condut = 7.7e-7*tg**0.75
      rk(l) = (3./(dens*cps*r))*(condut*tg/r+ef*sigma*tg**4)
507   if(l.eq.1) then
      tsolid(2)=tsolid(1)+deltim/2.*rk(1)
      elseif(l.eq.2) then
      tsolid(3)=
     &   tsolid(1)+0.2071*deltim*rk(1)+0.2929*deltim*rk(2)
      elseif(l.eq.3) then
      tsolid(4)=tsolid(1)-
     &  0.7071*deltim*rk(2)+1.7071*deltim*rk(3)
      endif
      enddo
      ts=tsolid(1)+(deltim/6.)*(rk(1)+.58578*rk(2)+3.41422*rk(3)+rk(4))
      y = ((1.-wl/100.)/(1.-swl/100.))**0.333
      rc = rp*y
      call combus(tg,ts,pt,rp,poxy,swl,y,densp,rate,cwl,phi,qrh,fash,
     & deltim,q1,q2)
      call cbstm(deltim,psteam,ph2,pco,rp,pt,densp,tg,ts,swl,y,fash,
     & rate2,cwl2,qcsm,wl) 
      call cbco2(pco2,deltim,wl,swl,rp,pt,densp,tg,ts,fash,y,rate3,
     & cwl3,qcbco2)

      ycr = ycr-(cwl+cwl2+cwl3)
      wl=wl+(cwl+cwl2+cwl3)*(1.+floxy+flh+fls+fln)*100.
 
C   Char-Oxygen reaction 
      doxy1 = (alpha+beta/4.-epsn/2.-gama/2.)*32.
      co2p1 = alpha*44.
      h2op1 = (beta/2.- epsn)*18.
      n2p1 = (delta/2.)*28.
      h2sp1 = epsn*34.

C   Char-steam reaction followed by CO and H2(product) combustion
      Doxy2 = (alpha+beta/4.-gama/2.-epsn/2.)*32.
      CO2P2 = alpha*44.
      h2oP2 = (beta/2.-epsn)*18.
      n2p2 = (delta/2.)*28.
      h2sp2 = epsn*34.
      
C   char-CO2 reaction followed by CO combustion
      Doxy3 = (alpha+beta/4.-gama/2.-epsn/2.)*32.
C   CO2 decreased by alpha but increased by two alphas
      co2p3 = alpha*44.
      h2op3 = (beta/2.-epsn)*18.
      n2p3 = (delta/2.)*28.
      h2sp3 = epsn*34.
      
C   partial pressure and flow rate calculation
      goxyex = goxy-fcoal*(cwl*doxy1+cwl2*doxy2+cwl3*doxy3)/(12.*alpha)
      if(goxyex .lt. 0.) goto 524
      goxy = goxyex
      cop = 0.
      gco = 0.
      pco = 0.
      rhtd = 0.
      goto 526
524   goxy = 0.
      cop = abs(goxyex)*(28./16.)/fcoal
      copmax = (28./12.)*(cwl*(2.-2./phi)+cwl2+cwl3)
      if(cop.le.copmax) goto 5251
      delh = delh/2.
      goto 300
5251  rhtd = cop*ht(2)/28.
526   gsteam =gsteam+fcoal*(cwl*h2op1+cwl2*h2op2+cwl3*h2op3)/(12.*alpha)
      gco2=gco2+fcoal*(cwl*co2p1+cwl2*co2p2+cwl3*co2p3)/12./alpha - 
     & fcoal*cop*(44./28.)
      gn2 = gn2+fcoal*(cwl*n2p1+cwl2*n2p2+cwl3*n2p3)/12./alpha
      gh2s=gh2s+fcoal*(cwl*h2sp1+cwl2*h2sp2+cwl3*h2sp3)/12./alpha
      gco = gco+fcoal*cop
      tmole = goxy/32.+gsteam/18.+gco2/44.+gn2/28.+gh2s/34.+gco/28.
      xcarb =100.*(gco/28.+gco2/44.+gch4/16.+gtar*6./78.)*12./(fcoal*yc)
      poxy = pt*goxy/(32.*tmole)
      psteam = pt*gsteam/(18.*tmole)
      pco2 = pt*gco2/(44.*tmole)
      pco = pt*gco/(28.*tmole)
      pn2 = pt*gn2/(28.*tmole)
      ph2s = pt*gh2s/(34.*tmole)
      gh2 = 0.
      ph2 = 0.
      gch4 = 0.
      pch4 = 0.

C     total heat of reaction generated in the gaseous phase = heat of combustion of 
C     co and hydrogen that produced from char-oxygen reaction, char-steam reaction, and 
C     char-co2 reaction
      rhta = (q1/12.+qrh)*cwl
      ht2q1 = (q1+qrh*12.)/(2.-2./phi) - ht(2)
      do n=1,2
      call heat(n,tg,ht)
      enddo
      rhtb = ht(1)*(alpha-gama+beta/2.-epsn)*cwl2/(alpha*12.)+
     & ht(2)*cwl2/12.
      rhtc =ht(1)*(beta/2.-gama-epsn)*cwl3/(12.*alpha)+2.*ht(2)*cwl3/12.
      rheat2 = rheat2+rhta+rhtb+rhtc-rhtd

      rhts = rhts+cwl*qrh-cwl2*qcsm/12.-cwl3*qcbco2/12.
      goto 700

560   if(wl.ge.99.95) wl = 99.95
      if(xcarb.ge.99.95) xcarb = 99.95
      if(poxy.ge.0.02) goto 8010
      condut = 7.7e-7*((tg+ts)**0.75)
      ct = -(3./(dens*cps*r))*(condut/r+ef*sigma*4.*tg**3)*deltim
      if(abs(ct).gt.25.) then
      ect = 1.0e-12
      else
      ect=exp(ct)
      endif
      ts = tg-(tg-ts)*ect
      call wgshift(ts,pco,psteam,pco2,ph2,rate4,qwg,gco,gsteam,gco2,
     & gh2,deltim,fcoal,fash,wgl,tg,pt)
C     wather-gas-shift reaction
      dco4 = wgl*fcoal*28.
      dh2o4 = wgl*fcoal*18.
      co2p4 = wgl*fcoal*44.
      h2p4 = wgl*fcoal*2.

      call ch4ref(ts,gch4,fcoal,deltim,dch46,qmref)

C     ***methane reforming reaction***
      dh2o6 = dch46*18./16.
      cop6 = dch46*28./16.
      h2p6 = 3.*dch46*2./16.

      rheat2 = rheat2+qmref*dch46/16.
      rheat3 = rheat3+qwg*wgl

      gsteam = gsteam-dh2o4-fcoal*dch46
      gco = gco-dco4+fcoal*cop6
      gh2 = gh2+h2p4+fcoal*h2p6
      gch4 = gch4-fcoal*dch46
      gco2 = gco2+co2p4
      if(gsteam.lt.0.)gsteam = 0.
      if(gco2.le.0.) gco2 = 0.
      if(gco.lt.0.) gco = 0.
      if(gh2.lt.0.) gh2 = 0.

      gsum = goxy+gsteam+gco2+gco+gh2+gh2s+gn2+gch4
      tmole=goxy/32.+gsteam/18.+gco2/44.+gco/28.+gh2/2.
     & +gh2s/34.+gn2/28.+gch4/16.
      psteam = pt*gsteam/(18.*tmole)
      pco2 = pt*gco2/(44.*tmole)
      pco = pt*gco/(28.*tmole)
      pn2 = pt*gn2/(28.*tmole)
      ph2 = pt*gh2/(2.*tmole)
      ph2s = pt*gh2s/(34.*tmole)
      pch4=pt*gch4/(16.*tmole)

      rhts = 0.
      goto 700

C     when oxygen is consumed

600   if(wl.ge.99.95) goto 560
      if(xcarb. ge.99.75) goto 560

      y = ((1.-wl/100.)/(1.-swl/100.))**0.333
      rc = rp*y
C     ***runge-kutta-gill method***
      tsolid(1)=ts
      do l=1,4
      ts = tsolid(l)
      if(ts.le.0.) goto 606
      call cbstm(deltim,psteam,ph2,pco,rp,pt,densp,tg,ts,swl,y,fash,
     & rate2,cwl2,qcsm,wl)
      call cbco2(pco2,deltim,wl,swl,rp,pt,densp,tg,ts,fash,y,
     & rate3,cwl3,qcbco2)
      call wgshift(ts,pco,psteam,pco2,ph2,rate4,qwg,gco,gsteam,gco2,
     & gh2,deltim,fcoal,fash,wgl,tg,pt)
      ratew = wgl*rp*densp/(3.*(1.+fash-swl/100.)*deltim)
      call cbhym(ph2,pch4,deltim,wl,swl,y,rp,densp,pt,tg,ts,cwl5,rate5,
     & qcbhy,fash)

C     cwl's are the gm carbon consumed per gm dmmf coal for each
C     specific reaction in each compartment
C     wgl is the gmole conversion for water-gas-shift reaction in each
C     compartment per gm dmmf coal
      condut = (7.7e-7)*((ts+tg)**0.75)
      rk(l)=(3./(dens*cps*r))*(condut*(tg-ts)/r+ef*sigma*(tg**4-ts**4)-
     &qwg*ratew*fwgs-rate2*qcsm/12.-rate3*qcbco2/12.-qcbhy*rate5/12.)
      if(wl.ge.95..and.abs(rk(l)).gt.8000.) goto 620
      goto 607
606   condut = 7.7e-7*(tg**0.75)
      rk(l)=(3./(dens*cps*r))*(condut*tg/r+ef*sigma*(tg**4))
607   if(l.eq.1) tsolid(2) = tsolid(1)+(deltim/2.)*rk(1)
      if(l.eq.2) tsolid(3) = tsolid(1)+0.2071*deltim*rk(1)
     & +0.2929*deltim*rk(2)
      if(l.eq.3) tsolid(4)=tsolid(1)-0.7071*deltim*rk(2)
     & +1.7071*deltim*rk(3)
      enddo
      ts = tsolid(1)+(deltim/6.)*(rk(1)+0.58578*rk(2)+
     & 3.41422*rk(3)+rk(4))
      goto 630

620   ts = ti
      condut = (7.7e-7)*((tg+ts)**0.75)
      ct=-(3./(dens*cps*r))*(condut/r+ef*sigma*4.*tg**3)*deltim
      if(abs(ct).gt.25.) then
      ect = 1.0e-12
      else
      ect = exp(ct)
      endif
      ts = tg-(tg-ts)*ect
630   y = ((1.-wl/100.)/(1.-swl/100.))**0.333
      rc = rp*y
      call cbstm(deltim,psteam,ph2,pco,rp,pt,densp,tg,ts,swl,y,
     & fash,rate2,cwl2,qcsm,wl)
      call cbco2(pco2,deltim,wl,swl,rp,pt,densp,tg,ts,fash,
     & y,rate3,cwl3,qcbco2)
      call wgshift(ts,pco,psteam,pco2,ph2,rate4,qwg,gco,gsteam,
     & gco2,gh2,deltim,fcoal,fash,wgl,tg,pt)
      call cbhym(ph2,pch4,deltim,wl,swl,y,rp,densp,pt,tg,ts,cwl5,
     & rate5,qcbhy,fash)

      wl = wl+(cwl2+cwl3+cwl5)*(1.+floxy+flh+fls+fln)*100.
      ycr = ycr-(cwl2+cwl3+cwl5)

C     char-steam reaction
      dh2o2 = (alpha-gama)*18.
      cop2 = alpha*28.
      h2p2 = (alpha-gama+beta/2.-epsn)*2.
      n2p2 = (delta/2.)*28.
      h2sp2 = epsn*34.

C     char-co2 reaction
      dco23 = alpha*44.
      cop3 = 2.*alpha*28.
      h2op3 = gama*18.
      h2p3 = (beta/2.-gama-epsn)*2.
      n2p3 = (delta/2.)*28.
      h2sp3 = epsn*34.

C     water-gas-shift reaction
      dco4 = wgl*fcoal*28.
      dh2o4 = wgl*fcoal*18.
      co2p4 = wgl*fcoal*44.
      h2p4 = wgl*fcoal*2.

C     carbon-hydrogen reaction
      dh25 = (2.*alpha+gama-epsn-beta/2.)*2.
      ch4p5 = alpha*16.
      h2op5 = gama*18.
      n2p5 = (beta/2.)*28.
      h2sp5 = epsn*34.

      call ch4ref(ts,gch4,fcoal,deltim,dch46,qmref)

C     ***methane reforming reaction***
      dh2o6 = dch46*18./16.
      cop6 = dch46*28./16.
      h2p6 = 3.*dch46*2./16.

C     partial pressure and flow rate calculation
      goxy = 0.
      poxy = 0.

      gsteam = gsteam+fcoal*(cwl3*h2op3-cwl2*dh2o2+h2op5*cwl5)/(12.*
     & alpha)-dh2o4-fcoal*dch46
      gco=gco+fcoal*(cwl2*cop2+cwl3*cop3)/(12.*alpha)-dco4+fcoal*cop6
      gh2 = gh2+fcoal*(cwl2*h2p2+cwl3*h2p3-cwl5*dh25)/(12.*alpha)+h2p4
     &+fcoal*h2p6
      gch4 = gch4+fcoal*cwl5*ch4p5/(12.*alpha)-fcoal*dch46
      gco2 = gco2-fcoal*cwl3*dco23/(12.*alpha)+co2p4
      gn2 = gn2+fcoal*(cwl2*n2p2+cwl3*n2p3+cwl5*n2p5)/(12.*alpha)
      gh2s=gh2s+fcoal*(cwl2*h2sp2+cwl3*h2sp3+cwl5*h2sp5)/(12.*alpha)

      if(gsteam .lt. 0.) gsteam = 0.
      if(gco2.le.0.) gco2 = 0.
      if(gco.le.0.) gco = 0.
      if(gh2.le.0.) gh2 = 0.

      tmole=gco2/44.+gsteam/18.+gco/28.+gn2/28.+gh2s/34.+gh2/2.+gch4/16.
      xcarb=100.*(gco/28.+gco2/44.+gch4/16.+gtar*6./78.)*12./(fcoal*yc)
      psteam = pt*gsteam/(18.*tmole)
      pco2 = pt*gco2/(44.*tmole)
      pco = pt*gco/(28.*tmole)
      pn2 = pt*gn2/(28.*tmole)
      ph2 = pt*gh2/(2.*tmole)
      ph2s = pt*gh2s/(34.*tmole)
      pch4 = pt*gch4/(16.*tmole)
      rheat2 = rheat2+qmref*dch46/16.
      rheat3 = rheat3+qwg*wgl*(1.-fwgs)
      rhts=rhts-qwg*wgl*fwgs-cwl2*qcsm/12.-cwl3*qcbco2/12.-
     & cwl5*qcbhy/12.

700   gsum = goxy+gsteam+gco2+gco+gh2+gh2s+gn2+gch4
      tmole = goxy/32.+gsteam/18.+gco2/44.+gco/28.+gh2/2.+
     & gh2s/34.+gn2/28.+gch4/16.
      
      if(wl.ge.99.95) wl = 99.95
      if(xcarb .ge. 99.95) xcarb = 99.95
      ti = ts
      r = rp
      dp = 2.*r
      dens = densi*(1.+fash-wl/100.)/((1.+fash)*swell)
      y = ((1.-wl/100.)/(1.-swl/100.))**0.333
      rc = rp*y
      call vsolid(delh,usi,tmole,gsum,dp,tg,pt,at,vis,dens,deltim,us,ug)
      usi = us
      time = time+deltim
      hs = hs+delh

      if(k .ge. nc) goto 515
      k = k+1
      if(goxy .gt. 0.) goto 505
      goxy = 0.
      poxy = 0.
      goto 600

515   rheat = rheat1+rheat2+rheat3 - rhts
      call enthal(tg,ts,fcoal,fash,wl,cps,enth)
      tohup = (enth-enthin)/fcoal

      if(poxyin. gt. 0.05) goto 736
      tw = 2100. - 600.*hs/height
      hloss = u0*3.14*dt*float(nc)*delh*(tg-tw)
      goto 737
736   hloss = fcoal*abs(rheat)*hl/100.
737   htexc = -rheat-tohup-hloss/fcoal 
      goto 420

C     correction of tg by half deltg
800   tg = tg+deltg/2.
      if(wl.ge.99.99) goto 8010
      !change 325 into height-5, using wison gasifier data
      dhs = hs-(height-5.)
      if(abs(dhs).gt.10.) goto 806
8010  write(6,801) M
801   format(///2x,'*****for gaseous compartment',i3,'***********')
      write(6,802)tg,time,ts,goxy,gsteam,gco2,gco,gn2,gh2,gh2s,poxy,
     & psteam,pco2,pco,pn2,ph2,ph2s,wl,hs,gch4,pch4
802   format(///2x,'corrected gas temp=',f15.5,//2x,'time=',e10.3,//2x,
     &'ts outlet=',f10.4,//2x,'oxygen flow rate=',f15.5,'gm/sec',//2x,
     &'steam flow rate=',f15.5,'g/sec',//2x,'co2 flow rate=',f15.5,'g/
     &sec,',//2x,'co flow rate=',f15.5,'gm/sec',//2x,'n2 flow rate=',
     &f15.5,'gm/sec',//2x,'h2 flow rate=',f15.5,'gm/sec',//2x,'h2s flow
     &rate=',f15.5,'gm/sec',//2x,'oxygen pressure=',f15.5,'atm',//2x,
     &'steam pressure=',f15.5,'atm',//2x,'co2 pressure=',f15.5,'atm',//
     &2x,'co pressure=',f15.5,'atm',//2x,'n2 pressure=',f15.5,'atm',//2x
     &,'hydrogen pressure=',f15.5,'atm',//2x,'h2s pressure=',f15.5,'atm'
     &,//2x,'total weight loss=',f15.5,'%',//2x,'entrained height=',
     &f15.5,'cm',//2x,'gch4=',f15.5,2x,'pch4=',f15.5,'atm')
      write(6,805) gtar
805   format(//2x,'tar yield=',f15.5,'g/sec')
      dryp = pt-psteam
      comol = (pco/dryp)*100.
      co2mol = (pco2/dryp)*100.
      h2mol = (ph2/dryp)*100.
      ch4mol = (pch4/dryp)*100.
      n2mol = (pn2/dryp)*100.
      h2smol = (ph2s/dryp)*100.
      write(6,862) comol,h2mol,co2mol,ch4mol,n2mol,h2smol
862   format(//2x,'co mol %=',f7.3,2x,'h2 mol %=',f7.3,2x,'co2 mol %='
     &,f7.3/2x,'ch4 mol %=',f7.3,2x,'n2 mol %=',f7.3,2x,'h2s mol %=',
     &f7.3)

808   format(/2x,'carbon conversion=',f10.4,2x,'%')

806   ip = m+1
      x1(ip,1) = hs
      x1(ip,2) = hs
      y1(ip,1) = tg
      y1(ip,2) = ts
      x2(ip,1) = hs
      x2(ip,2) = hs
      x2(ip,3) = hs
      x2(ip,4) = hs
      y2(ip,1) = psteam/pt
      y2(ip,2) = pco2/pt
      y2(ip,3) = pco/pt
      y2(ip,4) = ph2/pt
      x3(ip,1) = hs
      x3(ip,2) = hs
      x3(ip,3) = hs
      y3(ip,1) = pn2/pt
      y3(ip,2) = ph2s/pt
      y3(ip,3) = pch4/pt
      x4(ip,1) = hs
      y4(ip,1) = xcarb
      x5(ip,1) = hs
      x5(ip,2) = hs
      y5(ip,1) = ug
      y5(ip,2) = us

      if(wl.ge.99.99 .and. poxy.ge.0.02) goto 900
      if(tg.le.200.) goto 900
      if(ts.le.200.) goto 900
      if(m.eq.119) goto 840
      m = m+1
C     compartment sizie adjustment
      delh = 1.0
      if(us.le.100.) delh = 1.0
      if(poxy.gt.0.01) delh = 0.5
      if(hs.le.40.) delh = 0.5
      dwl = (swl-wl)/swl
      if(dwl.gt.0.08) delh = 0.5

      tgin = tg
      tsin = ts
      enthin = enth
      k = 1
      iterg = 1
      rheat1 = 0.
      rheat2 = 0.
      rheat3 = 0.
      rhts = 0.
      rheat = 0.
      subdwl = 0.
      usin = us
      goxyin = goxy
      gh2oin = gsteam
      gco2in = gco2
      gcoin = gco
      gh2in = gh2
      gch4in = gch4
      gh2sin = gh2s
      gn2in = gn2
      ph2oin = psteam
      poxyin = poxy
      pco2in = pco2
      pcoin = pco
      ph2in = ph2
      pch4in = pch4
      pn2in = pn2
      ph2sin = ph2s
      gtarin =gtar
      timein = time
      hsin = hs
      wlin = wl

      if(goxy .le. 0.) then
      tgas(1) = tgin - 10.
      tgas(2) = tgas(1) - 10.
      else
      tgas(1) = tgin + 50.
      tgas(2) = tgas(1) + 100.
      endif
      i = 1
      goto 300

C840   call fpplot(x1,y1,npt1,ic1,2,120)
C      call fpplot(x2,y2,npt2,ic2,4,120)
C      call fpplot(x3,y3,npt3,ic3,3,120)
C      call fpplot(x4,y4,npt4,ic4,1,120)
C      call fpplot(x5,y5,npt5,ic5,2,120)

840      goto 10
900   stop
      close(unit=2000)
      end
C*********************************subroutines*****************************************
      subroutine volatl(yc,yoxy,yh,yn,ys,yash,fc,fo,fh,fn,fs,vh2,r1,
     & r2,vm0)
      common /a2/xh2,xco,xco2,xh2s,xh2o,xn2,xch4,xtar
C     ***fc,fh,fn,fs are the % of o2,h2,n2,s respectively remained 
C     ***in char after devolatilization***
      fo = 5.
      fh = 10.
      fs = 50.
      fn = 50.
C     ***vh2 is the amount of h2 in volatile as % of dmmf coal, which can
C     be estimated by loison-chauvin correlation***
C     ***r1 = xco/xco2
C     ***r2 = xh2o/xco2, r1 and r2 can be estimated by loison-chauvin correlation
C     for a given coal***
      r1 = 2.0
      r2 = 2.667
      vh2 = 1.0

C     ***fc is the fixed carbon of the given dmmf coal, %***
      vc = yc*100.-fc
      vh = yh*(100.-fh)
      vn = yn*(100.-fn)
      vs = ys*(100.-fs)
      vo = yoxy*(100.-fo)
      vco2 = vo/(32./44.+r1*16./28.+r2*16./18.)
C     oxygen balance***
      vco = r1*vco2
      vh2o = r2*vco2
      vh2s = vs*34./32.
      vn2 = vn
C     ***hydrogen balance and carbon balance to solve vch4 and vtar***
      h = vh-vh2-vh2o*2./18.-vh2s*2./34.
      c = vc-vco*12./28.-vco2*12./44.
      vch4 = (12.*h-c)/(12.*4./16.-12./16.)
      vtar = (h-vch4*4./16.)/(6./78.)
      vm0 = vh2+vco+vco2+vh2o+vh2s+vn2+vch4+vtar
      xh2 = vh2/vm0
      xco = vco/vm0
      xco2 = vco2/vm0
      xh2o = vh2o/vm0
      xh2s = vh2s/vm0
      xn2 = vn2/vm0
      xch4 = vch4/vm0
      xtar = vtar/vm0
      write(6,100) xh2,xco,xco2,xh2s,xn2,xch4,xtar,xh2o,vm0
100   format(//2x,'xh2=',f10.4,2x,'xco=',f10.4,2x,'xco2=',f10.4,2x
     &,'xh2s=',f10.4,/2x,'xn2=',f10.4,2x,'xch4=',f10.4,2x,'xtar=',f10.4
     &,2x,'xh2o=',f10.4,2x,'vm0=',f10.4)

      return
      end

      subroutine vsolid(delh,usi,tmole,gsum,dp,tg,pt,at,vis,dens,
     & rdtime,us,ug)
     
C     for upward flow, 'direct' = 1.0
C     for downard flow, 'direct' = -1.0

C     downward flow
      parameter Rg = 82.06
      parameter g = 980.
      direct = -1.0

      ug = (tmole*Rg*tg)/(at*pt)
      deng = pt*gsum/(tmole*rg*tg)
      acont = 18.*vis/(dens*dp**2)
      ut = (dens-deng)*g*(dp**2)/(18.*vis)

      if(usi .eq. 0.) then
      us = ug-ut*direct
      else
      us = usi
      endif
      time = delh/us
30    bcont = -acont*time
      if(abs(bcont).le.25.) then
      eb = exp(bcont)
      else
      eb = 0.
      endif

      ftime=delh-usi*(1.-eb)/acont-(ug-ut*direct)*(time-(1.-eb)/acont)
      dftime=-usi*eb-(ug-ut*direct)*(1.-eb)
      dtime=ftime/dftime
      time=time-dtime
      if(abs(dtime).gt.0.02) goto 30
      rdtime = time
      us = -dftime
      return 
      end

      subroutine heat(n,temp,ht)
      dimension ht(20)
C     n=1 is H2 combustion, n=2 is co combustion, n=3 is ch4 combustion,
C     n=4 is c6h6 combustion
C     the unit of ht is cal/gmole
      dimension ht298(20),dalpha(20),dbeta(20),dgama(20)
      ht298(1)=-57798.
      ht298(2)=-66911.6
      ht298(3)=-191760.
      ht298(4)=-749420.
      dalpha(1)=-2.765
      dalpha(2)=-3.28
      dalpha(3)=5.049
      dalpha(4)=13.351
      dbeta(1)=0.947e-3
      dbeta(2)=7.18e-3
      dbeta(3)=-9.256e-3
      dbeta(4)=-3.1616e-2
      dgama(1)=2.636e-7
      dgama(2)=-2.8875e-6
      dgama(3)=9.501e-6
      dgama(4)=1.293e-5
      ht(n)=ht298(n)+dalpha(n)*(temp-298.)+dbeta(n)*(temp**2-298.**2)/2.
     & +dgama(n)*(temp**3-298.**3)/3.
      return
      end

      subroutine pyroly(wl,swl,goxy,rheat1,doxy,waterp,co2p,h2sp,
     & n2p,cop,h2p,ch4p,tarp)
      common /s1/ fcoal,foxy,fsteam /s2/ tg,ts,dens,deng,cps,pt
      common /a1/ yc,yoxy,yh,ys,yn,ycr,yoxyr,yhr,ysr,ynr
      common /a2/ xh2,xco,xco2,xh2s,xh2o,xn2,xch4,xtar
      dimension ht(20)
      real n2p

      do n=1,4
      call heat(n,tg,ht)
      enddo

      if(goxy.le.0.) then 
      h2p = wl*xh2/100.
      co2p=wl*xco2/100.
      cop=wl*xco/100.
      waterp=wl*xh2o/100.
      h2sp=wl*xh2s/100.
      n2p=wl*xn2/100.
      ch4p=wl*xch4/100.
      tarp=wl*xtar/100.
      rheat1=0.
      return
      endif

      doxy = wl*(xh2/4.+xco/56.+xch4/8.+xtar/10.4)*(32./100.)
      tarp = 0.
      goxyex=goxy-doxy*fcoal
      if(goxyex.lt.0.) goto 124
      cop=0.
      ch4p=0.
      h2p=0.
      goto 126
124   cop=(abs(goxyex)/fcoal)*(28./16.)
      copmax = wl*xco/100.
      if(cop.gt.copmax) goto 125
      h2p=0.
      ch4p=0.
      goto 126
125   ch4p=(cop-copmax)*(16./(4.*28.))
      cop=copmax
      ch4pmx=wl*xch4/100.
      if(ch4p.gt.ch4pmx) goto 128
      h2p=0.
      goto 126
128   h2p=(ch4p-ch4pmx)*(4.*2./16.)
      ch4p=ch4pmx
      h2pmax=wl*xh2/100.
      if(h2p.gt.h2pmax) h2p=h2pmax
126   co2p=wl*(xco2/44.+xco/28.+xch4/16.+xtar/13.)*(44./100.)-
     & cop*(44./28.)-ch4p*(44./16.)
      waterp=wl*(xh2/2.+xh2o/18.+xch4/8.+xtar/26.)*(18./100.)-
     & h2p*18./2.-ch4p*36./16.
      h2sp = xh2s*(wl/100.)
      n2p = xn2*(wl/100.)
      rheat1 = wl*(xh2*ht(1)/2.+xco*ht(2)/28.+xch4*ht(3)/16.+
     & xtar*ht(4)/78.)/100.-cop*ht(2)/28.-ch4p*ht(3)/16.-h2p*ht(1)/2.
      return 
      end

      subroutine heatup(n,temp,ti,htup,cp)
      parameter iamax = 30
      dimension alpha(iamax),beta(iamax),gama(iamax)
      dimension htup(iamax),cp(iamax)
C     n=1 is oxygen, n=2 is steam, n=3 is co2, n=4 is co
C     n=5 is ch4, n=6 is h2
C     n=7 is h2s, n=8 is n2
C     the unit of temp is K, the unit of htup is cal/gmole

      alpha(1)=6.148
      alpha(2)=7.256
      alpha(3)=6.214
      alpha(4)=6.42
      alpha(5)=3.381
      alpha(6)=6.947
      alpha(7)=6.662
      alpha(8)=6.524
      beta(1)=3.102e-3
      beta(2)=2.298e-3
      beta(3)=1.0396e-2
      beta(4)=1.665e-3
      beta(5)=1.8044e-2
      beta(6)=-0.2e-3
      beta(7)=5.134e-3
      beta(8)=1.25e-3
      gama(1)=-0.923e-6
      gama(2)=0.283e-6
      gama(3)=-3.545e-6
      gama(4)=-0.196e-6
      gama(5)=-4.3e-6
      gama(6)=0.481e-6
      gama(7)=-0.854e-6
      gama(8)=-0.001e-6
      cp(n)=alpha(n)+beta(n)*temp+gama(n)*(temp**2)
      htup(n)=alpha(n)*(temp-ti)+beta(n)*(temp**2-ti**2)/2.
     & +gama(n)*(temp**3-ti**3)/3.
      return
      end

      subroutine enthal(tg,ts,fcoal,fash,wl,cps,enth)
      parameter iamax = 30
      dimension htup(iamax),cp(iamax)
      dimension alpha(iamax),beta(iamax),gama(iamax)
      common /a3/goxy,gsteam,gco2,gco,gh2,gh2s,gn2,gch4
      do n=1,8
      call heatup(n,tg,298.,htup,cp)
      enddo
      enthg = htup(1)*goxy/32.+htup(2)*gsteam/18.+htup(3)*gco2/44.
     & + htup(4)*gco/28.+htup(5)*gch4/16.+htup(6)*gh2/2.+htup(7)*
     & gh2s/34. + htup(8)*gn2/28.
      enths = cps*fcoal*(1.+fash-wl/100.)*(ts-298.)
      enth = enthg+enths
      return
      end

      subroutine combus(tg,ts,pt,rp,poxy,swl,y,densp,rate,cwl,phi,qrh,
     & fash,deltim,q1,q2)
      real ks,kdiff,kdash,kover

      void = 0.75
      
      dp = 2.*rp
      tm = (ts+tg)/2.
      q1 = -94051. - 3.964*(ts-298.) + 3.077e-3*(ts**2-298.**2) -
     & 0.874e-6*(ts**3-298.**3)
      q2 = -26414. - 0.684*(ts-298.) - 0.513e-3*(ts**2-298.**2) +
     & 8.85e-8*(ts**3-298.**3)
      z = 2500.*exp(-6249./tm)
      if(dp. le. 5.0e-3) then
      phi = (2.*z+2.)/(z+2.)
      elseif(dp.le.0.1) then
      phi = ((2.*z+2.)-z*(dp-0.005)/0.095)/(z+2.)
      else
      phi = 1.0
      endif
      qrh = -(q1/12.)*(2./phi-1.)-(q2/12.)*(2.-2./phi)
      if(poxy .le. 0. .or. ts. lt. 273.) then
      rate = 0.
      cwl = 0.
      return
      endif
      ats = -17967./ts
      if(abs(ats).gt.49.) then
      eats = 1.e-15
      else
      eats = exp(ats)
      endif
      ks = 8710.*eats
      diff = (4.26/pt)*(tg/1800.)**1.75
      kdiff = (phi*0.292*diff)/(dp*tm)
      if(y .ge. 0.95) then
      kover = 1./(1./kdiff+1./ks)
      else
      kdash = kdiff*(void**2.5)
      kover = 1./(1./kdiff+1./(y*y*ks)+(1./kdash)*(1./y-1.))
      endif
      rate = kover*poxy
      cwl = rate*3.*(1.+fash-swl/100.)*deltim/(densp*rp)
      return
      end

      subroutine cbstm(deltim,psteam,ph2,pco,rp,pt,densp,tg,ts,swl,
     & y,fash,rate2,cwl2,qcsm,wl)
      real ks,kdiff,kdash,kover
      void = 0.75
      dp = 2.*rp
      if(psteam .lt. 0.001 .or. wl. ge. 99.9) goto 10
      if(ph2.le.0. .or. pco.le.0.) goto 5
      cts = 17.644 - 30260./(ts*1.8)
      if (abs(cts) .gt. 16.) goto 10
      cseqk = exp(cts)
      if(cseqk .gt. 10000.) goto 5
      pexc = psteam - ph2*pco/cseqk
      goto 6
5     pexc=psteam
6     if(pexc .le. 0.) goto 10
      goto 20
10    cwl2 = 0.
      rate2 = 0.
      qcsm = 0.
      goto 30 
20    tm = (ts+tg)/2.
      if(tm .le. 200.) goto 10
      ats = -21060./ts
      if (abs(ats) .gt. 25.) goto 10
      eats = exp(ats)
      ks = 247.*eats
      kdiff = (10.e-4)*(tm/2000.)**0.75/(dp*pt)
      if(y.ge.0.95) then
      kover = 1./(1./kdiff+1./ks)
      else
      kdash = kdiff*(void**2.5)
      kover = 1./(1./kdiff+1./(y*y*ks)+(1./kdash)*(1./y-1.))
      endif
      rate2 = kover*pexc
      cwl2 = rate2*3.*(1.+fash-swl/100.)*deltim/(densp*rp)
      qcsm = 31382.+2.011*(ts-298.)-(0.733e-3)*(ts**2-298.**2)/2.
30    return 
      end

      subroutine cbco2(pco2,deltim,wl,swl,rp,pt,densp,tg,ts,
     & fash,y,rate3,cwl3,qcbco2)
      real ks,kdiff,kdash,kover
          
      void = 0.75 
      
      dp = 2.*rp
      if(ts.le.850. .or. pco2.le.0. .or. wl.ge.99.9) goto 10
      tm = (ts+tg)/2.
      if(tm .gt. 200.) goto 100
10    cwl3=0.
      rate3=0.
      qcbco2=0.
      return
100   bts = -21060./ts
      if(abs(bts).gt.25) goto 10
      ebts = exp(bts)
      ks = 247.*ebts
      kdiff = (7.45e-4)*(tm/2000.)**0.75/(dp*pt)
      if(y.ge.0.95) then
      kover = 1./(1./kdiff+1./ks)
      else
      kdash = kdiff*(void**2.5)
      kover = 1./(1./kdiff+1./(y*y*ks)+1./kdash*(1./y-1.))
      endif
      rate3 = kover*pco2
      cwl3 = rate3*3.*(1.+fash-swl/100.)*deltim/(densp*rp)
      qcbco2 = 41220.+2.256*(ts-298.)-(7.066e-3)*(ts**2-298.**2)/2. + 
     & (3.153e-6)*(ts**3-298.**3)/3.
      return 
      end

      subroutine wgshift(ts,pco,psteam,pco2,ph2,rate4,qwg,gco,gsteam,
     & gco2,gh2,deltim,fcoal,fash,wgl,tg,pt)
C     water-gas shift reaction
      f = 0.2
      if(ts .le. 1000.) goto 10
      tm = (tg+ts)/2.
      ckwg = exp(-3.6893+7234./(1.8*tm))
      ek = exp(-27760./(1.987*ts))
      ep = 0.5-pt/250.
      pf = pt**ep
      rat = exp(-8.91+5553./ts)
      goto 20
10    rate4 = 0.
      qwg = 0.
      return
      
20    pexc = pco-pco2*ph2/(ckwg*psteam)
      rate4 = f*(2.877e5)*ek*(pexc/pt)*pf*rat
      wgl = rate4*deltim*fash
C     the unit of rate4 is gmole/sec-gm of ash

      a = 1.-ckwg
      b = gco2/44.+gh2/2.+ckwg*(gco/28.+gsteam/18.)
      c = (gco2/44.)*(gh2/2.)-ckwg*(gco/28.)*(gsteam/18.)
      d = b*b-4.*a*c
      if(d .lt. 0.) goto 10
      if(pexc .lt. 0.) then
      x = (-b-sqrt(d))/(2.*a)
      else
      x = (-b+sqrt(d))/(2.*a)
      endif
      wglmax = x/fcoal
      check = wgl*wglmax
      if(check .lt. 0.) goto 10
      if(abs(wgl).gt.abs(wglmax)) then
      wgl=wglmax
      rate4 = wgl/(deltim*fash)
      endif
      qwg = -9839.-0.515*(ts-298.)+(6.233e-3)*(ts**2-298.**2)/2. - 
     & (3.151e-6)*(ts**3-298.**3)/3.
      return
      end

      subroutine cbhym(ph2,pch4,deltim,wl,swl,y,rp,densp,pt,tg,ts,cwl5,
     & rate5,qcbhy,fash)
      
      real kv,keq,ks,kdiff,kdash,kover
      if(ts.le.1200. or. wl.ge.99.9 .or. wl.le.swl) goto 10
      void = 0.75
      dp = 2.*rp
      tm = (tg+ts)/2.

      if(ph2.le.0.001) goto 10
      if(pch4.le. 0.) goto 5
      ats = 18400./(1.8*ts)
      if(abs(ats).ge.25.) goto 5
      keq = (0.175/34713.)*exp(ats)
      peq = pch4/keq
      peqmax = ph2*ph2
      if(peq.ge.peqmax) goto 10
      peqmin = peqmax/10000.
      if(peq.le.peqmin) goto 5
      goto 6
5     pexc = ph2
      goto 7
6     pexc = ph2-sqrt(peq)
7     if(pexc.le.0.001) goto 10
      bts = -17921./ts
      if(abs(bts).ge.25.) goto 10
      ks = 0.12*exp(bts)
      kdiff = (1.33e-3)*(tm/2000.)**0.75/(dp*pt)
      if(y.ge.0.95) then
      kover = 1./(1./kdiff+1./ks)
      else
      kdash = kdiff*(void**2.5)
      kover = 1./(1./kdiff+1./(y*y*ks)+(1./kdash)*(1./y-1.))
      endif

      rate5 = kover*pexc
      cwl5 = rate5*3.*(1.+fash-swl/100.)*deltim/(densp*rp)
      qcbhy = -17889.-14.613*(ts-298.)+(1.7424e-2)*(ts**2-298.**2)/2.-
     & (5.26e-6)*(ts**3-298.**3)/3.
      goto 100
10    rate5 = 0.
      cwl5 = 0.
      qcbhy = 0.
100   return
      end

      subroutine ch4ref(ts,gch4,fcoal,deltim,dch46,qmref)
      if(ts.le.1000.) goto 10
      ek = 312.*exp(-30000./(1.987*ts))
      dch46 = gch4*(1.-exp(-ek*deltim))/fcoal
      qmref = 45126.55+16.624*ts-9.6385e-3*ts*ts+1.7346e-6*ts**3
      goto 50
10    dch46 = 0.
      qmref = 0.
50    return
      end

